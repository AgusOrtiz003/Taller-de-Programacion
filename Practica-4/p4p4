programa p4p4
procesos
  proceso juntarFlores
  comenzar
    mientras(HayFlorEnLaEsquina)
      tomarFlor
  fin
  proceso juntarPapeles
  comenzar
    mientras(HayPapelEnLaEsquina)
      tomarPapel
  fin
  proceso vaciarBolsa
  comenzar
    mientras(HayPapelEnLaBolsa)
      depositarPapel
    mientras(HayFlorEnLaBolsa)
      depositarFlor
  fin
  proceso enviarTarea(E tarea:numero)
  variables
    id,av,ca:numero
  comenzar
    Random(id,1,2)
    Random(av,2,100)
    Random(ca,2,100)
    si(id=1)
      EnviarMensaje(tarea,r1)
      EnviarMensaje(av,r1)
      EnviarMensaje(ca,r1)
    sino
      EnviarMensaje(tarea,r2)
      EnviarMensaje(av,r2)
      EnviarMensaje(ca,r2)
  fin
  proceso realizarTarea(E tarea:numero;E av:numero; E ca:numero)
  variables
    avI,caI:numero
  comenzar
    avI:=PosAv
    caI:=PosCa
    BloquearEsquina(av,ca)
    Pos(av,ca)
    si(tarea=1)
      juntarFlores
    sino
      si(tarea=2)
        juntarPapeles
      sino
        vaciarBolsa
    Pos(avI,caI)
    LiberarEsquina(av,ca)
  fin
areas
  cuadrante:AreaPC(2,2,100,100)
  areaJ:AreaP(1,1,1,1)
  area1:AreaP(2,1,2,1)
  area2:AreaP(3,1,3,1)
robots
  robot Jefe
  variables
    tarea:numero
  comenzar
    repetir 10
      Random(tarea,1,3)
      enviarTarea(tarea)
    EnviarMensaje(4,r1)
    EnviarMensaje(4,r2)
  fin
  robot Trabajador
  variables
    tarea,av,ca:numero
  comenzar
    RecibirMensaje(tarea,rJ)
    mientras(tarea<>4)
      RecibirMensaje(av,rJ)
      RecibirMensaje(ca,rJ)
      realizarTarea(tarea,av,ca)
      RecibirMensaje(tarea,rJ)
  fin
variables
  r1,r2:Trabajador
  rJ:Jefe
comenzar
  AsignarArea(r1,area1)
  AsignarArea(r2,area2)
  AsignarArea(rJ,areaJ)
  AsignarArea(r1,cuadrante)
  AsignarArea(r2,cuadrante)
  Iniciar(r1,2,1)
  Iniciar(r2,3,1)
  Iniciar(rJ,1,1)
fin
