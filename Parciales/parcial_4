programa parcial4
procesos
  proceso juntarFlores
  comenzar
    mientras(HayFlorEnLaEsquina)
      tomarFlor
  fin
  proceso pedirEsquina(E id:numero;ES av:numero;ES ca:numero)
  comenzar
    EnviarMensaje(id,rF)
    RecibirMensaje(av,rF)
    RecibirMensaje(ca,rF)
  fin
  proceso realizarEsquina(E av:numero;E ca:numero)
  variables
    avI,caI:numero
  comenzar
    avI:=PosAv
    caI:=PosCa
    BloquearEsquina(av,ca)
    Pos(av,ca)
    juntarFlores
    Pos(avI,caI)
    LiberarEsquina(av,ca)
  fin
  proceso identificarRobots
  comenzar
    EnviarMensaje(1,f1)
    EnviarMensaje(2,f2)
    EnviarMensaje(3,f3)
  fin
  proceso recibirYEnviarEsquina(ES intentos:numero;ES terminados:numero)
  variables
    id,av,ca:numero
  comenzar
    RecibirMensaje(id,*)
    si(id=0)
      terminados:=terminados+1
    sino
      Random(av,10,30)
      Random(ca,10,30)
      intentos:=intentos+1
      si(id=1)
        EnviarMensaje(av,f1)
        EnviarMensaje(ca,f1)
      sino
        si(id=2)
          EnviarMensaje(av,f2)
          EnviarMensaje(ca,f2)
        sino
          EnviarMensaje(av,f3)
          EnviarMensaje(ca,f3)                
  fin
areas
  cuadrante:AreaPC(10,10,30,30)
  area1:AreaP(1,1,1,1)
  area2:AreaP(2,1,2,1)
  area3:AreaP(3,1,3,1)
  areaF:AreaP(9,9,9,9)
robots
  robot Florero
  variables
    id,av,ca,n:numero
  comenzar
    RecibirMensaje(id,rF)
    Random(n,2,5)
    repetir n
      pedirEsquina(id,av,ca)
      realizarEsquina(av,ca)
    EnviarMensaje(0,rF)
  fin
  robot Fiscalizador
  variables
    terminados,intentos:numero
  comenzar
    identificarRobots
    terminados:=0
    intentos:=0
    mientras(terminados<3)
      recibirYEnviarEsquina(intentos,terminados)
    Informar('Entre todos hicieron',intentos)          
  fin
variables
  f1,f2,f3:Florero
  rF:Fiscalizador
comenzar
  AsignarArea(f1,area1)
  AsignarArea(f2,area2)
  AsignarArea(f3,area3)
  AsignarArea(rF,areaF)
  AsignarArea(f1,cuadrante)
  AsignarArea(f2,cuadrante)
  AsignarArea(f3,cuadrante)
  Iniciar(f1,1,1)
  Iniciar(f2,2,1)
  Iniciar(f3,3,1)
  Iniciar(rF,9,9)
fin
